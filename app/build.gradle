apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
	compileSdk 33

	defaultConfig {
		applicationId "github.daneren2005.dsub"
		minSdkVersion 26
		// TODO minSdk upgraded due to org.eclipse.jetty dependency. Target a lower level later.
		targetSdkVersion 31
		versionCode 208
		versionName '5.5.3'
		setProperty("archivesBaseName", "DSub $versionName")
        resConfigs 'de', 'es', 'fr', 'hu', 'nl', 'pt-rPT', 'ru', 'sv', 'zh'
        vectorDrawables.useSupportLibrary = true
		resValue 'string', 'account_type.subsonic', applicationId + ".subsonic"
		resValue 'string', 'provider.search', applicationId + ".provider.DSubSearchProvider"
		resValue 'string', 'provider.playlist', applicationId + ".playlists.provider"
		resValue 'string', 'provider.podcast', applicationId + ".podcasts.provider"
		resValue 'string', 'provider.starred', applicationId + ".starred.provider"
		resValue 'string', 'provider.recently_added', applicationId + ".mostrecent.provider"
	}
	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles 'proguard.cfg'
			zipAlignEnabled true
		}
		fix {
			minifyEnabled true
			shrinkResources true
			proguardFiles 'proguard.cfg'
			zipAlignEnabled true
		}
	}
	flavorDimensions "version"
	productFlavors {
		floss {
			// FLOSS build (no proprietary libraries)
			dimension "version"
		}
		google {
			// FLOSS build + Google libraries
			// Adds ChromeCast support
			dimension "version"
		}
	}
    packagingOptions {
        resources {
            excludes += ['META-INF/beans.xml']
        }
    }

	buildFeatures {
		viewBinding = true
	}

    signingConfigs {
		debug {
			storeFile file('../debug.keystore')
		}
	}
    namespace 'github.daneren2005.dsub'
    lint {
        checkReleaseBuilds false
    }
}

dependencies {
	implementation project(path: ':ServerProxy')
	implementation fileTree(include: ['*.jar'], dir: 'libs')

	// Kotlin extensions
	implementation("org.jetbrains.kotlin:kotlin-reflect:1.8.10")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
	implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0")

	// AndroidX
	implementation("androidx.core:core-ktx:1.9.0")
	implementation "androidx.activity:activity-ktx:1.7.0"
	implementation("androidx.appcompat:appcompat:1.6.1")
	implementation 'androidx.mediarouter:mediarouter:1.3.1'
	implementation("androidx.lifecycle:lifecycle-livedata-ktx:2.6.1")
	implementation("androidx.constraintlayout:constraintlayout:2.1.4")
	implementation("androidx.recyclerview:recyclerview:1.3.0")
	implementation("androidx.navigation:navigation-fragment-ktx:2.5.3")
	implementation("androidx.navigation:navigation-ui-ktx:2.5.3")
	implementation("androidx.preference:preference-ktx:1.2.0")
	implementation("androidx.databinding:databinding-runtime:7.4.2")
	implementation("androidx.mediarouter:mediarouter:1.3.1")
	implementation("androidx.fragment:fragment-ktx:1.5.6")

	implementation 'com.google.android.material:material:1.8.0'

	// Logging
	implementation("com.jakewharton.timber:timber:5.0.1")

	// Dependency injection
	implementation("io.insert-koin:koin-android:3.3.3")

	googleImplementation 'com.google.android.gms:play-services-cast:21.3.0'
	implementation 'com.sothree.slidinguppanel:library:3.0.0'
    implementation 'com.shehabic.droppy:Droppy:0.5.1@aar'
	implementation group: 'org.fourthline.cling', name: 'cling-core', version:'2.1.1'
	implementation group: 'org.fourthline.cling', name: 'cling-support', version:'2.1.1'
	implementation group: 'org.eclipse.jetty', name: 'jetty-server', version:'9.4.41.v20210516'
	implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'9.4.41.v20210516'
	implementation group: 'org.eclipse.jetty', name: 'jetty-client', version:'9.4.41.v20210516'
}
